PREFIX mat: <http://bimerr.iot.linkeddata.es/def/material-properties#> 
PREFIX mmo: <https://w3id.org/pmd/materials-mechanics-ontology/> 
PREFIX saref: <https://w3id.org/saref#> 
PREFIX bso: <https://w3id.org/bso#>
PREFIX cpo: <https://w3id.org/cpo#>
PREFIX inst:  <https://random-url-to-your-data.com/data#> 

DELETE{
	?CoSH saref:hasValue ?oldCoSHv .
}
INSERT{
    ?CoSH a mmo:SpecificHeat .
    ?C bso:hasMaterialProperty ?CoSH.
	?CoSH saref:hasValue ?CoSHv .
}
WHERE {
    ?C a mat:Concrete .
    ?CMD a mmo:MassDensity .
    
    ?CAM a cpo:CoarseAggregateMaterial .
    ?FAM a cpo:FineAggregateMaterial .
    ?W a cpo:Water .
    ?BM a cpo:BinderMaterial .
    
     
    ?CASH a mmo:SpecificHeat .
    ?FASH a mmo:SpecificHeat .
    ?WSH a mmo:SpecificHeat .
    ?BSH a mmo:SpecificHeat .
    
    ?CAQ a cpo:CoarseAggregateQuantity .
    ?FAQ a cpo:FineAggregateQuantity .
    ?WQ a cpo:WaterQuantity .
    ?BQ a cpo:BinderQuantity .
	
    ?C cpo:hasComponentQuantity ?CAQ .
    ?C cpo:hasComponentQuantity ?FAQ .
    ?C cpo:hasComponentQuantity ?WQ .
    ?C cpo:hasComponentQuantity ?BQ .
    
    ?C bso:hasMaterialProperty ?CMD .
	
    ?C cpo:isMadeOfComponentMaterial ?CAM .
    ?C cpo:isMadeOfComponentMaterial ?FAM .
    ?C cpo:isMadeOfComponentMaterial ?W .
    ?C cpo:isMadeOfComponentMaterial ?BM .

    ?CAM bso:hasMaterialProperty ?CASH .
    ?FAM bso:hasMaterialProperty ?FASH .
    ?W bso:hasMaterialProperty ?WSH .
    ?BM bso:hasMaterialProperty ?BSH .

    ?CAQ saref:hasValue ?CAQv .
    ?FAQ saref:hasValue ?FAQv .
    ?WQ saref:hasValue ?WQv .
    ?BQ saref:hasValue ?BQv .

    ?CASH saref:hasValue ?CASHv .
    ?FASH saref:hasValue ?FASHv .
    ?WSH saref:hasValue ?WSHv .
    ?BSH saref:hasValue ?BSHv .
	
    ?CMD saref:hasValue ?MDv .

    BIND (?CAQv * ?CASHv AS ?CApv) .
    BIND (?FAQv * ?FASHv AS ?FApv) .
    BIND (?WQv * ?WSHv AS ?Wpv) .
    BIND (?BQv * ?BSHv AS ?Bpv) .
    BIND (?CApv + ?FApv + ?Wpv + ?Bpv AS ?Copv) .
    BIND (?Copv / ?MDv AS ?CoSHv) .
	
	BIND (IRI(CONCAT(STR(?C), "-SpecificHeat")) AS ?CoSH) .
    
    OPTIONAL{
        ?CoSH a mmo:SpecificHeat .
    	?C bso:hasMaterialProperty ?CoSH.
    }
    
    OPTIONAL{
        ?CoSH a mmo:SpecificHeat .
    	?C bso:hasMaterialProperty ?CoSH .
	 	?CoSH saref:hasValue ?oldCoSHv .
    }
}
