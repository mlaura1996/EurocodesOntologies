PREFIX mat: <http://bimerr.iot.linkeddata.es/def/material-properties#> 
PREFIX mmo: <https://w3id.org/pmd/materials-mechanics-ontology/> 
PREFIX saref: <https://w3id.org/saref#> 
PREFIX bso: <https://w3id.org/bso#>
PREFIX cpo: <https://w3id.org/cpo#>
PREFIX inst:  <https://random-url-to-your-data.com/data#> 

DELETE{
    ?CoTC saref:hasValue ?oldCoTCv .
}
INSERT{
    ?CoTC a mmo:ThermalConductivity .
    ?C bso:hasMaterialProperty ?CoTC .
    ?CoTC saref:hasValue ?CoTCv .
}
WHERE {
    ?C a mat:Concrete .
   
    ?CAM a cpo:CoarseAggregateMaterial .
    ?FAM a cpo:FineAggregateMaterial .
    ?W a mat:Water .
    ?BM a cpo:BinderMaterial .
    
    ?CATC a mmo:ThermalConductivity .
    ?FATC a mmo:ThermalConductivity .
    ?WTC a mmo:ThermalConductivity .
    ?BTC a mmo:ThermalConductivity .
    
    ?CAQ a cpo:CoarseAggregateQuantity .
    ?FAQ a cpo:FineAggregateQuantity .
    ?WQ a cpo:WaterQuantity .
    ?BQ a cpo:BinderQuantity .
    
    ?CMD a mmo:MassDensity .

    ?C cpo:isMadeOfComponentMaterial ?CAM, ?FAM, ?W, ?BM .
    ?C cpo:hasComponentQuantity ?CAQ, ?FAQ, ?WQ, ?BQ .
    ?C bso:hasMaterialProperty ?CMD .

    ?CAM bso:hasMaterialProperty ?CATC .
    ?FAM bso:hasMaterialProperty ?FATC .
    ?W bso:hasMaterialProperty ?WTC .
    ?BM bso:hasMaterialProperty ?BTC .

    ?CAQ saref:hasValue ?CAQv .
    ?FAQ saref:hasValue ?FAQv .
    ?WQ saref:hasValue ?WQv .
    ?BQ saref:hasValue ?BQv .

    ?CATC saref:hasValue ?CATCv .
    ?FATC saref:hasValue ?FATCv .
    ?WTC saref:hasValue ?WTCv .
    ?BTC saref:hasValue ?BTCv .
    
    ?CMD saref:hasValue ?MDv .

    BIND (?CAQv * ?CATCv AS ?CApv) .
    BIND (?FAQv * ?FATCv AS ?FApv) .
    BIND (?WQv * ?WTCv AS ?Wpv) .
    BIND (?BQv * ?BTCv AS ?Bpv) .
    BIND (?CApv + ?FApv + ?Wpv + ?Bpv AS ?Copv) .
    BIND (?Copv / ?MDv AS ?CoTCv) .
    
    BIND (IRI(CONCAT(STR(?C), "-ThermalConductivity")) AS ?CoTC) .
    
    OPTIONAL{
        ?CoTC a mmo:ThermalConductivity .
    	?C bso:hasMaterialProperty ?CoTC .
    }
    
    OPTIONAL{
        ?CoTC a mmo:ThermalConductivity .
    	?C bso:hasMaterialProperty ?CoTC .
    	?CoTC saref:hasValue ?oldCoTCv 
    }
}
