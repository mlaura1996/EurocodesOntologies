PREFIX mat: <http://bimerr.iot.linkeddata.es/def/material-properties#>
PREFIX mmo: <https://w3id.org/pmd/materials-mechanics-ontology/>
PREFIX saref: <https://w3id.org/saref#>
PREFIX bso: <https://w3id.org/bso#>
PREFIX cpo: <https://w3id.org/cpo#>
PREFIX inst: <https://random-url-to-your-data.com/data#>

DELETE {
    ?MD saref:hasValue ?oldMDv .
}
INSERT {
    ?MD a mmo:MassDensity .
    ?MD saref:hasValue ?MDv .
    ?C bso:hasMaterialProperty ?MD .
}
WHERE {
    ?BQ a cpo:BinderQuantity .
    ?CAQ a cpo:CoarseAggregateQuantity .
    ?FAQ a cpo:FineAggregateQuantity .
    ?WQ a cpo:WaterQuantity .

    ?C cpo:hasComponentQuantity ?BQ, ?CAQ, ?FAQ, ?WQ .

    ?BQ saref:hasValue ?BQv .
    ?CAQ saref:hasValue ?CAQv .
    ?FAQ saref:hasValue ?FAQv .
    ?WQ saref:hasValue ?WQv .

    BIND (?BQv + ?CAQv + ?FAQv + ?WQv AS ?MDv) .
    BIND (IRI(CONCAT(STR(?C), "-MassDensity")) AS ?MD) .

    OPTIONAL {
        ?C bso:hasMaterialProperty ?MD .
        ?MD a mmo:MassDensity .
    }
    
    OPTIONAL {
        ?C bso:hasMaterialProperty ?MD .
        ?MD a mmo:MassDensity .
        ?MD saref:hasValue ?oldMDv .
    }
}